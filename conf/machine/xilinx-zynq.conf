#@TYPE: Machine
#@NAME: xilinx-zynq
#@DESCRIPTION: Machine configuration for Xilinx Zynq-7000 SoC

DEFAULTTUNE ?= "armv7athf-neon"
require conf/machine/include/arm/armv7a/tune-cortexa9.inc

BSP_XILINX_DERIVED:append = " xilinx-zynq"

PREFERRED_PROVIDER_virtual/kernel ??= "linux-yocto"
PREFERRED_VERSION_linux-yocto ?= "6.1%"
PREFERRED_VERSION_linux-yocto-rt ?= "6.1%"

KERNEL_DEVICETREE = "zynq-zc706.dtb \
                                 zynq-zc702.dtb "

MACHINE_FEATURES = "usbhost jffs2 ext3 pci alsa profile"
#QSPI flash has 64kB erase size
EXTRA_IMAGECMD:jffs2 = "-lnp -e64KiB"

KERNEL_IMAGETYPE = "zImage"

SERIAL_CONSOLE = "115200 ttyPS0"

MACHINE_EXTRA_RRECOMMENDS = " kernel-modules kernel-devicetree"
IMAGE_FSTYPES += "tar.bz2 wic wic.bmap"
WKS_FILE = "zynq-sdimage.wks"
IMAGE_INSTALL:append = " kernel-devicetree kernel-image-zimage"
do_image_wic[depends] += "mtools-native:do_populate_sysroot dosfstools-native:do_populate_sysroot"

# variables needed by xilinx-bootbin package
MACHINEOVERRIDES:prepend = "zynq:"
SOC_FAMILY ?= "zynq"
UBOOT_ELF ?= "u-boot.elf"
UBOOT_MACHINE ?= "xilinx_zynq_virt_defconfig"

IMAGE_BOOT_FILES ?= "zImage zynq-zc702.dtb zynq-zc706.dtb"

# For ostree
# No u-boot
OSTREE_BOOTLOADER_INCLUDE ?= ""
OSTREE_SD_UBOOT_WIC1 ?= ""

INITRAMFS_IMAGE ??= ""
IMAGE_BOOT_FILES:append = " ${@bb.utils.contains('DISTRO_FEATURES', 'ostree', 'boot.scr', '', d)} ${@bb.utils.contains('DISTRO_FEATURES', 'ostree', '${INITRAMFS_IMAGE}-${MACHINE}.${INITRAMFS_FSTYPES};initramfs', '', d)}"

OSTREE_SD_BOOT_ALIGN ?= "4096"
OSTREE_FDISK_BLM ?= "8192"
OSTREE_FDISK_FSZ ?= "132"
OSTREE_KERNEL ?= "zImage"
OSTREE_UBOOT_CMD ?= "bootz"
OSTREE_WKS_EFI_SIZE ?= "--size=64M"
DEFAULT_DTB ?= "zynq-zc706.dtb"
OSTREE_COPY_IMAGE_BOOT_FILES = "1"

OSTREE_BOOTSCR_PRECMD ?= "\
setenv no_menu yes; \
setenv no_setexpr yes; \
setenv loadaddr 0x3000000; \
setenv fdt_addr 0x2a00000; \
setenv initrd_addr 0x6000000; \
setenv console ttyPS0; \
setenv baudrate 115200; \
"

# For ustart.img
OSTREE_NETINST_DEV ?= "/dev/mmcblk0,/dev/sda,/dev/vda"
OSTREE_NETINST_ARGS ?= "${OSTREE_CONSOLE} bl=ufsd rdinit=/install instdev=${OSTREE_NETINST_DEV} instname=wrlinux instbr=\$BRANCH insturl=\$URL instab=${OSTREE_USE_AB} instsf=1 instdate=BUILD_DATE"
