#@TYPE: Machine
#@NAME: amd-zynq
#@DESCRIPTION: Machine configuration for AMD Zynq-7000 SoC

DEFAULTTUNE ?= "armv7athf-neon"
require conf/machine/include/arm/armv7a/tune-cortexa9.inc
require ${@bb.utils.contains_any('DISTRO_FEATURES', 'ostree', 'conf/machine/zynq-ostree.conf', '', d)}

BSP_XILINX_DERIVED:append = " amd-zynq"

PREFERRED_PROVIDER_virtual/kernel ??= "linux-yocto"
PREFERRED_VERSION_linux-yocto ?= "6.6%"
PREFERRED_VERSION_linux-yocto-rt ?= "6.6%"

KERNEL_DEVICETREE = "xilinx/zynq-zc706.dtb xilinx/zynq-zc702.dtb "

MACHINE_FEATURES = "usbhost jffs2 ext3 pci alsa profile"
#QSPI flash has 64kB erase size
EXTRA_IMAGECMD:jffs2 = "-lnp -e64KiB"

KERNEL_IMAGETYPE = "zImage"

SERIAL_CONSOLE = "115200 ttyPS0"

MACHINE_EXTRA_RRECOMMENDS = " kernel-modules kernel-devicetree"
IMAGE_FSTYPES += "tar.bz2 wic wic.bmap"
WKS_FILE = "zynq-sdimage.wks"
IMAGE_INSTALL:append = " kernel-devicetree kernel-image-zimage"
do_image_wic[depends] += "mtools-native:do_populate_sysroot dosfstools-native:do_populate_sysroot"

# variables needed by xilinx-bootbin package
MACHINEOVERRIDES:prepend = "zynq:"
SOC_FAMILY ?= "zynq"
UBOOT_ELF ?= "u-boot.elf"
UBOOT_MACHINE ?= "xilinx_zynq_virt_defconfig"

IMAGE_BOOT_FILES ?= "zImage zynq-zc702.dtb zynq-zc706.dtb"
